plugins {
	id 'com.github.johnrengelman.shadow' version '7.1.0'
	id "org.beryx.jlink" version "2.25.0"
}

targetCompatibility = JavaVersion.VERSION_17
sourceCompatibility = JavaVersion.VERSION_17

shadowJar {
	//I'd like to minimize, but it is very aggressive and only saves 1M right now
//	minimize{
//		exclude(dependency('org.apache.groovy:.*:.*'))
//	}
	archiveFileName = 'Leikr.jar'
	//Exclude Asset directories from inside the jar.
	exclude 'Data'
	exclude 'Programs'
	exclude 'Leikr'
	exclude 'Leikr.bat'
}

task deployLeikr {
	dependsOn shadowJar
	doLast {
		println "From $projectDir/../assets"
		println "To $projectDir/../deploy"
		copy {
			from "$projectDir/build/libs"
			into "$projectDir/../deploy"
			exclude "desktop.jar"
		}
		copy {
			from "$projectDir/../assets"
			into "$projectDir/../deploy"
			exclude "*gamecontrollerdb.txt*"
			exclude "*Internal"
		}
	}
}

mainClassName = "leikr.desktop.DesktopLauncher"
project.ext.assetsDir = new File("$projectDir/../assets")

sourceSets.main.resources.srcDirs = [project.ext.assetsDir.getAbsolutePath()]

task launchDesktop(dependsOn: classes, type: JavaExec) {
	mainClass = project.mainClassName
	classpath = sourceSets.main.runtimeClasspath
	standardInput = System.in
	workingDir = project.assetsDir
	ignoreExitValue = true
	//args 'insecure'//Security is now an opt-out feature
}
