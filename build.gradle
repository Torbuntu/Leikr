buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
    dependencies {
        classpath 'org.mini2Dx:butler:1.1.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

allprojects {

    version = '0.0.5'
    ext {
        appName = 'Leikr'
        mini2DxVersion = '1.9.3'
        gdxVersion = '1.9.10'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
    
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

project(":core") {
    apply plugin: "java"

    dependencies {
        implementation "org.mini2Dx:mini2Dx-core:$mini2DxVersion"
        implementation "org.mini2Dx:mini2Dx-tiled:$mini2DxVersion"
        implementation "org.lwjgl.lwjgl:lwjgl:2.9.3"
        
        implementation "org.codehaus.groovy:groovy:3.0.0-beta-1"
        implementation "org.apache.commons:commons-io:1.3.2"
    }
}

project(":desktop") {
    apply plugin: "java"
    apply plugin: "application"
    apply plugin: "org.mini2Dx.butler"

    dependencies {
        implementation project(":core")
        implementation "org.mini2Dx:mini2Dx-desktop:$mini2DxVersion"
        
        implementation "org.apache.commons:commons-io:1.3.2"
    }
    butler {
        user = "Torbuntu"
        game = "Leikr"
        windows {
            binDirectory = "$buildDir/windows"
        }
        osx {
            binDirectory = "$buildDir/mac"
        }
        linux {
            binDirectory = "$buildDir/linux"            
        }        
    }
}
